plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-parcelize'

  id 'androidx.navigation.safeargs.kotlin'

  id 'kotlin-kapt'
  id 'dagger.hilt.android.plugin'

  id 'io.gitlab.arturbosch.detekt'
}

// for reading local.properties
def properties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
  properties.load(localPropertiesFile.newDataInputStream())
}

def hnBaseUrl = properties.getProperty('hn.baseUrl') != null
    ? properties.getProperty('hn.baseUrl') : ""
def hnApiVersion = properties.getProperty('hn.apiVersion') != null
    ? properties.getProperty('hn.apiVersion') : ""

android {
  compileSdk 31

  defaultConfig {
    applicationId "com.mohammadfayaz.news"
    minSdk 21
    targetSdk 31
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "com.mohammadfayaz.hn.HiltTestRunner"
  }

  buildTypes {

    buildTypes.each {
      it.buildConfigField("String", "hnBaseUrl", "\"${hnBaseUrl}\"")
      it.buildConfigField("String", "hnApiVersion", "\"${hnApiVersion}\"")
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  buildFeatures {
    viewBinding true
  }
}

dependencies {

  implementation(libs.bundles.androidxCore)
  implementation(libs.bundles.androidxNavigation)
  implementation(libs.bundles.androidxLifecycle)
  implementation(libs.bundles.google)
  implementation(libs.bundles.jakewharton)
  implementation(libs.bundles.retrofit)
  implementation(libs.bundles.mikepenz)
  implementation(libs.bundles.androidxViews)
  implementation(libs.bundles.androidxCache)
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'

    kapt(libs.bundles.kapt)

  testImplementation(libs.bundles.testDependencies)
  kaptTest(libs.bundles.daggerTestsAnnotation)
  kaptAndroidTest(libs.bundles.daggerTestsAnnotation)

  androidTestImplementation(libs.bundles.testDependencies)

  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
